<DOCTYPE html>
    <html><head><meta charset="UTF-8">
        <title>Voronoi Basic</title></head>
    <body style="margin:0; background-color: black">
    <canvas id="webGLCanvas" width="1440" height="900" style="width: 1440px; height: 900px">-</canvas>
    <script id="voronoi-frag-shader" type="x-shader/x-fragment">
precision lowp int;
precision highp float;

uniform vec2 resolution;
uniform float time;

float noise3D(vec3 p)
{
//smootherstep
//x*x*x*(x*(x*6.-15.)+10.)
    return fract(sin(dot(p ,vec3(12.9898,78.233,128.852))) * 43758.5453)*2.0-1.0;
}
float simplex3D(vec3 p)
{
//http://webstaff.itn.liu.se/~stegu/simplexnoise/simplexnoise.pdf
float f3 = 1.0/3.0;
float s = (p.x+p.y+p.z)*f3;
int i = int(floor(p.x+s));
int j = int(floor(p.y+s));
int k = int(floor(p.z+s));

float g3 = 1.0/6.0;
float t = float((i+j+k))*g3;
float x0 = float(i)-t;
float y0 = float(j)-t;
float z0 = float(k)-t;
x0 = p.x-x0;
y0 = p.y-y0;
z0 = p.z-z0;

int i1,j1,k1;
int i2,j2,k2;

if(x0>=y0)
{
 if(y0>=z0){ i1=1; j1=0; k1=0; i2=1; j2=1; k2=0; } // X Y Z order
 else if(x0>=z0){ i1=1; j1=0; k1=0; i2=1; j2=0; k2=1; } // X Z Y order
 else { i1=0; j1=0; k1=1; i2=1; j2=0; k2=1; }  // Z X Z order
}
else
{
 if(y0<z0) { i1=0; j1=0; k1=1; i2=0; j2=1; k2=1; } // Z Y X order
 else if(x0<z0) { i1=0; j1=1; k1=0; i2=0; j2=1; k2=1; } // Y Z X order
 else { i1=0; j1=1; k1=0; i2=1; j2=1; k2=0; } // Y X Z order
}

float x1 = x0 - float(i1) + g3;
float y1 = y0 - float(j1) + g3;
float z1 = z0 - float(k1) + g3;
float x2 = x0 - float(i2) + 2.0*g3;
float y2 = y0 - float(j2) + 2.0*g3;
float z2 = z0 - float(k2) + 2.0*g3;
float x3 = x0 - 1.0 + 3.0*g3;
float y3 = y0 - 1.0 + 3.0*g3;
float z3 = z0 - 1.0 + 3.0*g3;

vec3 ijk0 = vec3(i,j,k);
vec3 ijk1 = vec3(i+i1,j+j1,k+k1);
vec3 ijk2 = vec3(i+i2,j+j2,k+k2);
vec3 ijk3 = vec3(i+1,j+1,k+1);

vec3 gr0 = normalize(vec3(noise3D(ijk0),noise3D(ijk0*2.01),noise3D(ijk0*2.02)));
vec3 gr1 = normalize(vec3(noise3D(ijk1),noise3D(ijk1*2.01),noise3D(ijk1*2.02)));
vec3 gr2 = normalize(vec3(noise3D(ijk2),noise3D(ijk2*2.01),noise3D(ijk2*2.02)));
vec3 gr3 = normalize(vec3(noise3D(ijk3),noise3D(ijk3*2.01),noise3D(ijk3*2.02)));

float n0 = 0.0;
float n1 = 0.0;
float n2 = 0.0;
float n3 = 0.0;

float t0 = 0.5 - x0*x0 - y0*y0 - z0*z0;
if(t0>=0.0)
{
 t0*=t0;
 n0 = t0 * t0 * dot(gr0, vec3(x0, y0, z0));
}
float t1 = 0.5 - x1*x1 - y1*y1 - z1*z1;
if(t1>=0.0)
{
 t1*=t1;
 n1 = t1 * t1 * dot(gr1, vec3(x1, y1, z1));
}
float t2 = 0.5 - x2*x2 - y2*y2 - z2*z2;
if(t2>=0.0)
{
 t2 *= t2;
 n2 = t2 * t2 * dot(gr2, vec3(x2, y2, z2));
}
float t3 = 0.5 - x3*x3 - y3*y3 - z3*z3;
if(t3>=0.0)
{
 t3 *= t3;
 n3 = t3 * t3 * dot(gr3, vec3(x3, y3, z3));
}
return 96.0*(n0+n1+n2+n3);

}
vec4 circle(vec2 circleCenter, vec2 uv, float radius)
{
    float clampedDistance = clamp((length(circleCenter - uv) - radius)*256., 0., 1.);
    return vec4(0., .5, 1., 1. - clampedDistance);
}
void main()
{
    vec2 uv = gl_FragCoord.xy/resolution.xy;
    float screenRatio = resolution.x / resolution.y;
    uv.x *= screenRatio;
    uv.x += -time*.05 + simplex3D(vec3(0.,0.,(time+3.)*.02));
    uv.y +=  -time*.05 + simplex3D(vec3(0.,0.,(time+50.)*.03));

    vec2 localUV = uv * 12.;
    vec2 id = floor(localUV);
    localUV = fract(localUV);

    vec4 circleShape;
    vec2 circleCenter = vec2(0);

    float minimumDistance = 1.;
    vec2 closestPointID = id;

    for(int column = -1; column < 2; column++)
    {
       for(int row = -1; row < 2; row++)
        {
            vec2 idOffset = vec2(row, column);

            circleCenter.x = (simplex3D(vec3(id + idOffset, time *.35))+1.)*.5;
            circleCenter.y = (simplex3D(vec3(id + idOffset, time *.57 + 50.))+1.)*.5;
            float uvDistance = length(localUV-(circleCenter + idOffset));

            if(uvDistance < minimumDistance)
            {
                minimumDistance = uvDistance;
                closestPointID = id + idOffset;
            }
        }
    }
    vec3 color = vec3((simplex3D(vec3(closestPointID, time*.35+12.))+1.)*.15+.35, (simplex3D(vec3(closestPointID, time*.15+22.))+1.)*.25+.2, .5);

    //color *= (1.-minimumDistance*.15)+.5;


    //Smoother Step
    color *= 1.0-minimumDistance*minimumDistance*minimumDistance*(minimumDistance*(minimumDistance*6.-15.)+10.);
    color *= 1.5;
    gl_FragColor = vec4(color,  1.);
}
    </script>
    <script type="module">
        import {compileShader, createProgram, initBasicVertexBuffer} from "./js/Shader_Program_Utilities.js";
        import {basicVertexShader} from './js/Shader_Source.js';

        const canvas = document.querySelector("#webGLCanvas");
        const context = canvas.getContext("webgl");

        // canvas.width = window.innerWidth; canvas.height = window.innerHeight;

        let vertexShader = compileShader(context, context.VERTEX_SHADER, basicVertexShader);
        let voronoiFragShader = compileShader(context, context.FRAGMENT_SHADER, document.getElementById("voronoi-frag-shader").textContent);
        let voronoiProgram = createProgram(context, vertexShader, voronoiFragShader);
        context.useProgram(voronoiProgram);

        let vertexBufferParams = initBasicVertexBuffer(context, context.getAttribLocation(voronoiProgram, 'vertexPosition'));

        context.uniform2f(context.getUniformLocation(voronoiProgram, 'resolution'), window.innerWidth, window.innerHeight);

        requestAnimationFrame(update);

        function update(time)
        {
            context.uniform1f(context.getUniformLocation(voronoiProgram, 'time'), time*.001);

            context.clear(context.COLOR_BUFFER_BIT);
            context.drawArrays(context.TRIANGLE_STRIP,0 ,vertexBufferParams.size * .5);

            requestAnimationFrame(update);
        }

    </script>
    </body>
    </html>
</DOCTYPE>